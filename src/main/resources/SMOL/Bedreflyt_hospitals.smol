class HospitalModel extends BaseModel ()
    override Unit adaptAddition()
        List<Hospital> newHospitals = construct("
        PREFIX : <http://www.smolang.org/bedreflyt/>
        PREFIX brick: <https://brickschema.org/schema/Brick#>

        SELECT DISTINCT ?hospitalName ?hospitalCode ?cityName
        WHERE {
            ?hospital a brick:Hospital ;
                :hospitalName ?hospitalName ;
                :hospitalCode ?hospitalCode .
        }");

        if newHospitals != null then
            print("RECONFIG> Adding new hospitals...");

            while newHospitals != null do
                Hospital newHospital = newHospitals.content;
                List<Hospital> lx = newHospitals;
                newHospitals = newHospitals.next;

                newHospital.hospitalCity = newHospital.getHospitalCity();
                print("RECONFIG> Adding hospital " ++ newHospital.hospitalName);
                print("          Hospital city: " ++ newHospital.hospitalCity.cityName);
                destroy(lx);
            end

            print("RECONFIG> New hospitals added");
        end
    end

    override Unit adaptRemoval()
        List<Hospital> wrongHospitals = access("
        PREFIX : <http://www.smolang.org/bedreflyt/>
        PREFIX brick: <https://brickschema.org/schema/Brick#>

        SELECT DISTINCT ?obj {
            ?obj a prog:Hospital ;
                prog:Hospital_hospitalName ?hospitalName ;
                prog:Hospital_hospitalCode ?hospitalCode .
            FILTER NOT EXISTS {
                ?hospital a brick:Hospital ;
                    :hospitalName ?hospitalName ;
                    :hospitalCode ?hospitalCode .
            }
        }");

        if wrongHospitals != null then
            print("RECONFIG> Removing wrong hospitals...");

            while wrongHospitals != null do
                Hospital wrongHospital = wrongHospitals.content;
                List<Hospital> lx = wrongHospitals;
                wrongHospitals = wrongHospitals.next;

                print("RECONFIG> Removing hospital " ++ wrongHospital.hospitalName);
                destroy(wrongHospital);
                destroy(lx);
            end

            print("RECONFIG> Wrong hospitals removed");
        end
    end

    override Unit adaptDefect()
        skip;
    end

    override Unit adaptRepair()
        skip;
    end
end